\programVariables{
  R A; /* walle's maximum acceleration */
  R B; /* walle's maximum braking */
  R T; /* Time-trigger limit on evolution */
  R t;  /* time */

  R theta_w; /* Position of walle in y direction */
  R z_w; /* Position of walle in z direction */
  R dz_w; /* Unit vector in direction of travel, z direction */
  R r_w; /* Over-approximation on radius of walle */
  R omega_w; /* Angular velocity of walle */
  R a; /* Linear acceleration of walle */

  R theta_e; /* Position of eve in y direction */
  R z_e; /* Position of eve in z direction */
  R dz_e; /* Unit vector in direction of travel, z direction */
  R r_e; /* Over-approximation on radius of eve */
  R omega_e; /* Angular velocity of eve */

  R cute_r; /* Distance between center of walle and center of eve */

  R time_to_brake; 
  R dist_to_brake; 
  R time_to_acc; 
  R dist_to_acc; 
}

\problem{
(
omega_w >= 0 & omega_e > 0 &
0 < theta_e - theta_w & theta_e - theta_w < 360 &
T > 0 &
r_w > 0 & r_e > 0 &
dz_w > 0 & dz_e = dz_w & B > 0 & A > 0 &
(omega_w > omega_e -> T <= (omega_w - omega_e)/B) &
(omega_e > omega_w -> T <= (omega_e - omega_w)/A)
)
->
\[(
   a := *;
   time_to_brake := ((omega_w + a*T) - omega_e)/B;
   dist_to_brake := ((omega_w + a*T)^2 - omega_e^2)/(2*B);
   time_to_acc := (omega_e - (omega_w + a*T))/A;
   dist_to_acc := (omega_e^2 - (omega_w + a*T)^2)/(2*A);
   ?( a >= -B & a <= A &
     0 < theta_e + omega_e*T + omega_e*Max(time_to_brake, time_to_acc) -
     (theta_w + omega_w*T + a*T^2/2 + Max(dist_to_brake, dist_to_acc)) &
     theta_e + omega_e*T + omega_e*Max(time_to_acc, time_to_brake) - (theta_w + omega_w*T + a*T^2/2 + Max(dist_to_brake, dist_to_acc)) < 360);
   t := 0;
   {
     theta_w' = omega_w, z_w' = dz_w, omega_w' = a,
     theta_e' = omega_e, z_e' = dz_e,
     t' = 1, t <= T, omega_w >= 0
    }@invariant(
                 t >= 0
                )
   )*@invariant(
                omega_w >= 0 &
                0 < theta_e - theta_w &
                theta_e - theta_w < 360 & B > 0 & A > 0 &
                (omega_w > omega_e -> T <= (omega_w - omega_e)/B) &
                (omega_e > omega_w -> T <= (omega_e - omega_w)/A)
                )
\](
theta_e != theta_w
)

}
